// https://petrakeas.medium.com/publish-a-multi-module-java-android-library-to-maven-central-github-ci-automation-at-2021-4f98a311df2a
//
//apply plugin: 'maven-publish'

javadoc.options.addStringOption('Xdoclint:none', '-quiet')

group = 'com.net128.oss.web'
version = System.getProperty("VERSION", System.getenv("VERSION") ?: '0.0.1-SNAPSHOT')
sourceCompatibility = '11'

task javadocJar(type: Jar) {
    archiveClassifier.set('javadoc')
    from javadoc
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

publishing {
    publications {
        bootJava(MavenPublication) {
            //from components.java
            groupId = 'com.net128.oss.web'

            artifact tasks.named("bootJar")
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = artifactId
                artifactId = 'app.demo'
                packaging = 'jar'

                description = '''\
                    Web modules provide pluggable functionality to web applications. 
                    Typically a web module consists of a server and a client library.
                    '''
                url = 'https://github.com/mike-seger/web-modules/'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'ms'
                        name = 'Michael Seger'
                        email = 'mike-001@net128.com<'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/mike-seger/web-modules.git'
                    developerConnection = 'scm:git:git@github.com:mike-seger/web-modules.git'
                    url = 'https://github.com/mike-seger/web-modules/'
                }
            }
        }
    }

    repositories {
        maven {
            name = version.endsWith('SNAPSHOT') ? "ossrh" : "ossrhSnapshot"
            def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = ossrhUsername
                password = ossrhPassword
            }
        }
    }
}

//https://docs.gradle.org/current/userguide/signing_plugin.html
signing {
    //sign publishing.publications.release

    sign publishing.publications.bootJava
}

